apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "io.merculet"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        buildConfigField "boolean", "CHINA_ENABLE", "Boolean.parseBoolean(\"${CHINA_ENABLE}\")"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 多个jar包里包含了同样的文件（NOTICE.txt）,导致打包时因为担心相互覆盖问题而提示出错, 去掉这2个文件
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility = "1.7"
        targetCompatibility = "1.7"
    }

    signingConfigs { // 签名，你可以配置多个签名，然后再在buildTypes 进行指定。
        release {
            storeFile file("../zxInsight.jks") // 签名文件存放路径
            storePassword "zxInsight"
            keyAlias "magicWindowExample"
            keyPassword "magicWindowExample"
        }
        debug {
            storeFile file("../zxInsight.jks") // 签名文件存放路径
            storePassword "zxInsight"
            keyAlias "magicWindowExample"
            keyPassword "magicWindowExample"
        }
    }

    lintOptions { abortOnError false }

    // 每个环境包名不同
    productFlavors {
        //国内版包名
        ch {
            applicationId "cn.magicwindow.uav.sdk"
            signingConfig signingConfigs.release
        }
        //国际版包名
        en {
            applicationId "io.merculet.uav.sdk"
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.4.0'
    compile project(':MerculetSDK')
//    compile 'cn.magicwindow:uav-sdk:1.0.0'
}

/** 替换为国内版包名 **/
task replaceStringName(type: Copy) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('gradle.properties').newDataInputStream())
    def china_enable = properties.getProperty('CHINA_ENABLE', null).toBoolean()
    if (china_enable) { //是否是国内版
        //编译期间执行了,需要时打开
        FileTree tree = fileTree(dir: 'src/main')
        tree.include '**/*.java'
        tree.include '**/*.xml'
        tree.each { File mfile ->
            fileReader(mfile.path, "io.merculet.uav.sdk", "cn.magicwindow.uav.sdk")    // io.merculet.uav.sdk
        }
    }
}

def fileReader(path, oldStr, newStr) {
    def readerString = ""
    def hasReplace = false
    file(path).withReader('UTF-8') { reader ->
        reader.eachLine {
            if (it.find(oldStr) && !it.find(newStr)) {  //会出现重复替换
                it = it.replace(oldStr, newStr)
                hasReplace = true
            }
            readerString <<= it
            readerString << '\n'
        }
        if (hasReplace) {
            file(path).withWriter('UTF-8') {
                within -> within.append(readerString)
            }
        }
    }
}
